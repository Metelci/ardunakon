plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
    id 'jacoco'
    id 'org.jlleitschuh.gradle.ktlint' version '12.1.2'
    alias(libs.plugins.owasp.dependencycheck)
    alias(libs.plugins.hilt.android)
    alias(libs.plugins.ksp)
}

android {
    namespace = 'com.metelci.ardunakon'
    compileSdk = 35

    defaultConfig {
        applicationId = "com.metelci.ardunakon"
        minSdk = 26
        targetSdk = 35
        versionCode = 26
        versionName = "0.2.7-alpha-hotfix2"

        // Use custom test runner for Hilt
        testInstrumentationRunner = "com.metelci.ardunakon.ArdunakonTestRunner"
        vectorDrawables {
            useSupportLibrary = true
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "ENABLE_ANALYTICS", "false"
            buildConfigField "boolean", "ENABLE_CRASH_REPORTING", "false"
        }
        release {
            minifyEnabled = true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "ENABLE_ANALYTICS", "true"
            buildConfigField "boolean", "ENABLE_CRASH_REPORTING", "true"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose = true
        buildConfig = true
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
            excludes += 'META-INF/LICENSE.md'
            excludes += 'META-INF/LICENSE-notice.md'
        }
    }

    lint {
        abortOnError = false
        warningsAsErrors = false
        checkReleaseBuilds = false
    }
}

dependencies {
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.lifecycle.runtime.compose)
    implementation(libs.androidx.lifecycle.viewmodel.compose)
    implementation(libs.androidx.activity.compose)

    implementation(platform(libs.androidx.compose.bom))
    implementation(libs.androidx.compose.ui)
    implementation(libs.androidx.compose.ui.graphics)
    implementation(libs.androidx.compose.ui.tooling.preview)
    implementation(libs.androidx.compose.material3)
    implementation(libs.androidx.compose.material.icons.extended)

    implementation(libs.androidx.security.crypto)

    // Hilt Dependencies
    implementation(libs.hilt.android)
    implementation(libs.androidx.hilt.navigation.compose)
    ksp(libs.hilt.compiler)

    testImplementation(libs.junit4)
    testImplementation(libs.coroutines.test)
    testImplementation(libs.androidx.test.core.ktx)
    testImplementation(libs.robolectric)
    testImplementation(libs.mockk)
    testImplementation(libs.hilt.android.testing)
    kspTest(libs.hilt.compiler)

    androidTestImplementation(libs.androidx.test.ext.junit)
    androidTestImplementation(libs.espresso.core)
    androidTestImplementation(platform(libs.androidx.compose.bom))
    androidTestImplementation(libs.androidx.compose.ui.test.junit4)
    androidTestImplementation(libs.mockk.android)

    // Hilt Testing
    androidTestImplementation(libs.hilt.android.testing)
    kspAndroidTest(libs.hilt.compiler)

    debugImplementation(libs.androidx.compose.ui.tooling)
    debugImplementation(libs.androidx.compose.ui.test.manifest)
}

jacoco {
    toolVersion = "0.8.11"
}

// Allow Jacoco to instrument inline Kotlin classes.
tasks.withType(Test).configureEach {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
    testLogging {
        events "started", "passed", "skipped", "failed"
    }
}

tasks.register("jacocoTestReport", JacocoReport) {
    dependsOn "testDebugUnitTest"

    reports {
        xml.required = true
        html.required = true
    }

    def fileFilter = [
        '**/R.class',
        '**/R$*.class',
        '**/BuildConfig.*',
        '**/Manifest*.*',
        '**/*Test*.*',
        'android/**/*',
        '**/databinding/**/*',
        '**/Hilt_*',
        '**/*$lambda$*',
        '**/*Companion*',
        '**/*$WhenMappings*'
    ]

    // Use ASM-transformed classes to match what unit tests execute against (AGP 8+).
    def asmTree = fileTree(
        dir: "$buildDir/intermediates/classes/debug/transformDebugClassesWithAsm/dirs",
        excludes: fileFilter
    )
    classDirectories.from = files(asmTree)
    sourceDirectories.from = files(
        "$projectDir/src/main/java",
        "$projectDir/src/main/kotlin"
    )
    executionData.from = fileTree(
        dir: buildDir,
        includes: ['**/jacoco/testDebugUnitTest.exec']
    )
}

ktlint {
    android = true
    ignoreFailures = false
    reporters {
        reporter "plain"
        reporter "checkstyle"
        reporter "html"
    }
    filter {
        exclude("**/generated/**")
        include("**/kotlin/**")
    }
}

// OWASP Dependency Check configuration
dependencyCheck {
    formats = ['HTML', 'JSON']
    suppressionFile = file("$projectDir/owasp-suppressions.xml").exists()
        ? "$projectDir/owasp-suppressions.xml"
        : null
    failBuildOnCVSS = 8  // Fail on high/critical vulnerabilities
    analyzers {
        assemblyEnabled = false  // Disable .NET analyzer
        nuspecEnabled = false
        nodeEnabled = false
    }
}
