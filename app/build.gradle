plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'jacoco'
    // id 'org.jlleitschuh.gradle.ktlint' version '12.1.2'
    id 'org.owasp.dependencycheck' version '9.0.9'
}

android {
    namespace 'com.metelci.ardunakon'
    compileSdk 36

    defaultConfig {
        applicationId "com.metelci.ardunakon"
        minSdk 26
        targetSdk 36
        versionCode 23
        versionName "0.2.6-alpha"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "ENABLE_ANALYTICS", "false"
            buildConfigField "boolean", "ENABLE_CRASH_REPORTING", "false"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "ENABLE_ANALYTICS", "true"
            buildConfigField "boolean", "ENABLE_CRASH_REPORTING", "true"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.9.4'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.10.0'
    implementation 'androidx.activity:activity-compose:1.12.1'
    implementation platform('androidx.compose:compose-bom:2025.12.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material-icons-extended'
    implementation 'androidx.security:security-crypto:1.1.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.10.2'
    testImplementation 'androidx.test:core-ktx:1.7.0'
    testImplementation 'org.robolectric:robolectric:4.16'
    testImplementation 'io.mockk:mockk:1.13.13'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2025.12.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}

jacoco {
    toolVersion = "0.8.11"
}

// Allow Jacoco to instrument inline Kotlin classes.
tasks.withType(Test).configureEach {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

tasks.register("jacocoTestReport", JacocoReport) {
    dependsOn "testDebugUnitTest"

    reports {
        xml.required = true
        html.required = true
    }

    def fileFilter = [
        '**/R.class',
        '**/R$*.class',
        '**/BuildConfig.*',
        '**/Manifest*.*',
        '**/*Test*.*',
        'android/**/*',
        '**/databinding/**/*',
        '**/Hilt_*',
        '**/*$lambda$*',
        '**/*Companion*',
        '**/*$WhenMappings*'
    ]

    def javaTree = fileTree(
        dir: "$buildDir/intermediates/javac/debug/classes",
        excludes: fileFilter
    )
    def kotlinTree = fileTree(
        dir: "$buildDir/tmp/kotlin-classes/debug",
        excludes: fileFilter
    )

    classDirectories.from = files(javaTree, kotlinTree)
    sourceDirectories.from = files(
        "$projectDir/src/main/java",
        "$projectDir/src/main/kotlin"
    )
    executionData.from = fileTree(
        dir: buildDir,
        includes: ['**/jacoco/testDebugUnitTest.exec']
    )
}

// Ktlint configuration
// Ktlint configuration disabled
// ktlint {
//     version = "1.5.0"
//     android = true
//     ignoreFailures = false
//     reporters {
//         reporter "plain"
//         reporter "checkstyle"
//         reporter "html"
//     }
//     filter {
//         exclude("**/generated/**")
//         include("**/kotlin/**")
//     }
// }

// OWASP Dependency Check configuration
dependencyCheck {
    formats = ['HTML', 'JSON']
    suppressionFile = file("$projectDir/owasp-suppressions.xml").exists()
        ? "$projectDir/owasp-suppressions.xml"
        : null
    failBuildOnCVSS = 9  // Only fail on critical vulnerabilities
    analyzers {
        assemblyEnabled = false  // Disable .NET analyzer
        nuspecEnabled = false
        nodeEnabled = false
    }
}
